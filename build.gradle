import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id "idea"
    id "java-library"
    id "maven-publish"
    id "signing"
    id "org.jetbrains.kotlin.jvm" version "2.1.10"
}

ext {
    major = 1
    minor = 0
    patch = 1

    isCiServer = System.getenv("GITHUB_ACTIONS") != null || System.getProperty("GITHUB_ACTIONS") != null
}

group = "io.viascom.nanoid"
version = "${major}.${minor}.${patch}${isCiServer ? "" : "-SNAPSHOT"}"
project.logger.lifecycle("Version of this build: ${version}")

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

repositories {
    mavenCentral()
}

dependencies {
    //====== Kotlin Dependencies ======
    api("org.jetbrains.kotlin:kotlin-stdlib")

    //====== Testing Dependencies ======
    testImplementation("org.junit.jupiter:junit-jupiter:5.11.4")
    testImplementation("org.assertj:assertj-core:3.27.3")
    testImplementation("org.jetbrains.kotlin:kotlin-test:2.1.10")
}

tasks.withType(KotlinCompile).configureEach {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_1_8)
        freeCompilerArgs.add("-Xjvm-default=all")
    }
}

jar {
    manifest {
        attributes(
                "Implementation-Version": "${project.version}"
        )
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

publishing {
    repositories {
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"

            name = "OSSRH"
            url = isCiServer ? releaseRepo : snapshotRepo

            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME") ?: ""
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD") ?: ""
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                groupId = "io.viascom.nanoid"
                name = "nanoid"
                description = "Nano ID for Kotlin."
                url = "https://github.com/viascom/nanoid-kotlin"
                packaging = "jar"

                licenses {
                    license {
                        name = "Apache-2.0 license"
                        url = "https://www.apache.org/licenses/LICENSE-2.0"
                    }
                }

                scm {
                    url = "https://github.com/viascom/nanoid-kotlin"
                    connection = "scm:git://github.com/viascom/nanoid-kotlin.git"
                    developerConnection = "scm:git://github.com/viascom/nanoid-kotlin.git"
                }

                developers {
                    developer {
                        id = "itsmefox"
                        name = "Patrick BÃ¶sch"
                        email = "patrick.boesch@viascom.email"
                        organizationUrl = "https://viascom.io/"
                    }
                    developer {
                        id = "nik-sta"
                        name = "Nikola Stankovic"
                        email = "nikola.stankovic@viascom.email"
                        organizationUrl = "https://viascom.io/"
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign).configureEach {
    onlyIf { isCiServer }
}

tasks.test {
    useJUnitPlatform()
}

sourceSets {
    test {
        java.srcDirs("src/test/java", "src/test/kotlin")
    }
}
